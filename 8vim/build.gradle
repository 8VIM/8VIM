plugins {
    id 'org.jetbrains.kotlin.android' version '1.8.10'
    id "de.mannodermaus.android-junit5" version "1.9.3.0"
}

apply plugin: 'com.android.application'
apply plugin: 'checkstyle'

assemble.dependsOn('lint')
check.dependsOn('checkstyle')

android {
    compileSdkVersion 33

    defaultConfig {

        applicationId "inc.flide.vi8"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 11
        versionName "iteration-11"

        vectorDrawables.useSupportLibrary = true

        resValue "string", "version_name", versionName
    }
    signingConfigs {
        release {
            storeFile file(System.getenv().getOrDefault("VIM8_BUILD_KEYSTORE_FILE", "keystore"))
            storePassword System.getenv("VIM8_BUILD_KEYSTORE_PASSWORD")
            keyAlias System.getenv("VIM8_BUILD_KEY_ALIAS")
            keyPassword System.getenv("VIM8_BUILD_KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8

    }
    lint {
        abortOnError false
        disable 'ObsoleteLintCustomCheck', 'ClickableViewAccessibility', 'VectorPath'
        htmlOutput file('lint-report.html')
        htmlReport true
        warningsAsErrors false
    }
    namespace 'inc.flide.vim8'
}

dependencies {
    def junitVersion = "5.9.3"
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.14.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "androidx.preference:preference:1.2.0"
    implementation 'com.github.kizitonwose.colorpreference:support:1.1.0'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'org.apache.commons:commons-text:1.7'
    testImplementation "net.jqwik:jqwik:1.7.3"
    testImplementation "org.junit.jupiter:junit-jupiter:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    testImplementation "org.mockito:mockito-junit-jupiter:5.3.1"
    testImplementation "org.assertj:assertj-core:3.24.2"
    api 'com.afollestad.material-dialogs:core:0.9.2.3'
}
